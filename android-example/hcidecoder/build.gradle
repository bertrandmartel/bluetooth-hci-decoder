apply plugin: 'com.android.application'

import org.apache.tools.ant.taskdefs.condition.Os

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "fr.bmartel.android.hcidecoder"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        ndk { moduleName "hciwrapper" }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets.main {
        jni.srcDirs = [] // This prevents the auto generation of Android.mk
        jniLibs.srcDir 'src/main/libs'
        // This is not necessary unless you have precompiled libraries in your project.
    }


    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        def dirSeparator = "/"
        def dirname = "ndk-build"
        if (Os.isFamily(Os.FAMILY_WINDOWS)){
            dirSeparator = "\\"
            dirname = "ndk-build.cmd"
        }
        commandLine "${ndkDir}${dirSeparator}${dirname}",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        def dirSeparator = "/"
        def dirname = "ndk-build"
        if (Os.isFamily(Os.FAMILY_WINDOWS)){
            dirSeparator = "\\"
            dirname = "ndk-build.cmd"
        }
        commandLine "${ndkDir}${dirSeparator}${dirname}",
                '-C', file('src/main/jni').absolutePath, // Change src/main/jni the relative path to your jni source
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    applicationVariants.all { variant ->

        variant.outputs.each { output ->
            def file = output.outputFile
            output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + defaultConfig.versionName + "-" + getDateTime() + ".apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

import java.text.DateFormat
import java.text.SimpleDateFormat

def getDateTime() {
    DateFormat df = new SimpleDateFormat("ddMMYYYY");

    return df.format(new Date());
}